rem ********************************************************************
rem 無線AP優先度変更ツール
rem ********************************************************************
@echo off

rem ********************************************************************
rem 変数の初期化
rem ********************************************************************
:startssidmgr
set INTERFACE_NAME="Wi-Fi"
set NUM=
set SSID=
set RSID=
set IFNAME=
set PRIORITY=
set Rtry=

rem ********************************************************************
rem 対話式メニュー表示
rem ********************************************************************
cls
echo ----------------メニュー----------------
echo [1] 登録済み無線APの優先順位を確認
echo [2] 無線APの優先順位の変更
echo [3] 登録済みの無線LANプロファイル削除
echo [4] この処理を終了する
echo -----------------------------------------
set /p NUM="実行する処理が記載された番号を入力してください >"
if "%NUM%"=="1" (
  goto showaplist
) else if "%NUM%"=="2" (
  goto apmod
) else if "%NUM%"=="3" (
  goto apDel
) else if "%NUM%"=="4" (
  goto endMsg
) else (
  goto NoNumber
)

rem ********************************************************************
rem [1] 優先順位の確認
rem ********************************************************************
:showaplist
netsh wlan show profiles
pause
goto startssidmgr

rem ********************************************************************
rem [2] 優先順位の変更前確認
rem ********************************************************************
:apmod
cls
netsh wlan show profiles
echo;
echo ※上から順に1(優先度高)〜n(優先度低)
echo;
set /p SSID="優先順位を変更するプロファイル名を入力してください。 >"
set /p PRIORITY="変更後の優先順位を数字で入力してください。 >"

:ExecSSIDmod
echo;
echo このホスト（%COMPUTERNAME%）で利用する無線LANプロファイル"%SSID%"の
echo 優先順位を"%PRIORITY%"に変更します。
echo;
set /p Rtry="変更の実行は[Y]、キャンセルは[N]を入力しEnterキーを押下してください。 >"
if /i %Rtry% == y (goto startSSIDmod)
if /i %Rtry% == n (goto startssidmgr)
echo;
echo [Y]esか[N]oを入力してEnterキーを押下してください。
echo;
goto ExecSSIDmod

rem ********************************************************************
rem [2] 優先順位の変更処理
rem ********************************************************************
:startSSIDmod
netsh wlan set profileorder name="%SSID%" interface="%INTERFACE_NAME%" priority=%PRIORITY%
echo;
echo --------------------------------------------------
echo "%SSID%"の優先順位が"%PRIORITY%"になりました。
echo --------------------------------------------------
echo;
netsh wlan show profiles

rem ********************************************************************
rem 再帰処理の確認
rem ********************************************************************
echo --------------------------------------------------------------
set /p Rtry="処理を継続するには[Y]、この処理を終了するには[N]を入力しEnterキーを押下してください。 >"
echo %CONTINUE%
if /i %Rtry% == y (goto apmod)
if /i %Rtry% == n (goto startssidmgr)

rem ********************************************************************
rem [3] 登録済み無線LANプロファイル削除前確認
rem ********************************************************************
:apDel
cls
netsh wlan show profile
echo;
set /p SSID="削除するプロファイル名を入力してください。 >"
set /p RSID="確認のためもう一度入力してください。 >"
if %SSID%==%RSID% goto ExecDel
echo;
echo 入力に誤りがあります。入力を確認してください。
echo;
goto apDel

:ExecDel
echo;
echo このホスト（%COMPUTERNAME%）から無線LANプロファイル名"%SSID%"を削除します。
echo;
set /p Rtry="削除実行は[Y]、キャンセルは[N]を入力しEnterキーを押下してください。 >"
if /i %Rtry% == y (goto startapDel)
if /i %Rtry% == n (goto startssidmgr)
echo;
echo [Y]esか[N]oを入力してEnterキーを押下してください。
echo;
goto ExecDel

rem ********************************************************************
rem [3] 登録済み無線LANプロファイル削除処理
rem ********************************************************************
:startapDel
netsh wlan delete profile name="%SSID%"
echo;
echo ------------------------------------
echo "%SSID%"のプロファイル削除処理が完了しました。
echo ------------------------------------
echo;
netsh wlan show profiles

rem ********************************************************************
rem 再帰処理の確認
rem ********************************************************************
echo --------------------------------------------------------------
set /p Rtry="処理を継続するには[Y]、この処理を終了するには[N]を入力しEnterキーを押下してください。 >"
echo %CONTINUE%
if /i %Rtry% == y (goto apDel)
if /i %Rtry% == n (goto startssidmgr)

rem ********************************************************************
rem 対話式メニューの例外処理
rem ********************************************************************
:NoNumber
echo;
echo 指定された番号を入力してください。
echo;
pause
goto startssidmgr

rem ********************************************************************
rem 終了処理
rem ********************************************************************
:endMsg
exit 0